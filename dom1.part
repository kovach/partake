# Dominion

### Fundamentals

### Move
# (unstated) to move a card, change `it`'s location to `to`
move: +located ~it -> ~to.

### Stacking
# Place bottom of stack ~above onto top of stack ~below
stack-onto:
  ~move [ ~it := ~above.stack/bottom, ~to := ~below.stack/top ].

# Slide stack ~below underneath stack ~above.
stack-below:
  ~stack-onto,
  ~move [ ~it := ~below.stack/bottom, ~to := ~above ].

shuffle-stack-onto:
  # Choose one card from the source (halt if none remain).
  choose [random 1] (above X ~pile),
  # If more cards are on top of it, move them back to the pile.
  ( located C -> X, +located C -> ~pile ),
  # Move the chosen card to the temporary spot
  ~move [ ~it := X, ~to := ~onto ],
  # Continue shuffling with the newly chosen card as the
  # location for the next one.
  ~shuffle-stack-onto [ ~onto := X ].

shuffle-stack:
  +stack L, # temporary stack
  ~shuffle-stack-onto [ ~onto := L ],
  ~move [ ~it := L.stack/bottom, ~to := ~pile ],
  .

### Setup
game: ~setup, ~start-game.

# (unstated) Each player has a deck, a hand, a discard pile, a play area.
setup: ~setup-players, ~setup-decks.
setup-players:
  +player 'me,
  +player 'you,
  ( player P,
    +deck P Deck,
      +stack Deck, +stack Disc,
    +hand P Hand,
    +discard-pile P Disc,
    +play-area P _,
  ).

#    Each player takes 3 Estates and 7 Coppers and
#    shuffles them face down as a starting deck.
setup-decks: player P,
  ~setup-deck [ ~player := P ],
  ~shuffle-stack [~pile := P.deck ].

setup-deck:
  ~deal-estate, ~deal-estate, ~deal-estate,
  ~deal-copper, ~deal-copper, ~deal-copper, ~deal-copper,
  ~deal-copper, ~deal-copper, ~deal-copper,
  .

deal-estate: +estate C, +located C -> ~player.deck.
deal-copper: +copper C, +located C -> ~player.deck.

#    Randomly choose a starting player.
#    Each player draws an initial hand of 5 cards.
start-game:
  choose [random 1] (player Starter),
  +starting-player Starter,
  ( player P, ~draw-hand [ ~player := P ] ),
  ~turn [ ~player := Starter ].

#    Draw a new hand of 5 cards.
#    If your deck has fewer than 5 cards,
#      first shuffle your discard pile and put it under your deck,
#    then draw.
draw-hand:
  ( if (cards-in-deck ~player -> N, @lt N 5),
      ~shuffle-stack [ ~stack := ~player.discard ],
      ~stack-below [ ~above := ~player.deck,
                     ~below := ~player.discard ] ),

  ~draw-number [ ~number := 5 ].

draw-number:
  @lt 0 ~number, ~draw-card,
  ~draw-number [ ~number := @sub(~number, 1) ].

# To draw: move the top card of your deck to your hand (if it exists).
draw-card:
  stack/top ~player.deck C,
  +located C -> ~player.hand.

#    Players take turns.
#    Each turn has three phases: Action, then Buy, then Clean-up, which you can remember as ABC.
turn: ~action-phase, ~buy-phase, ~clean-up-phase.

#    In your Action phase, you can play one Action card from your hand.
action-phase:
  choose [upto 1] (located C -> ~player.hand,
                   action C),
  ~play [ ~card := C ].

exit.
